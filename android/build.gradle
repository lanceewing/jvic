apply plugin: 'com.android.application'

def version = rootProject.file('assets/data/version.txt').text.trim().substring(1);

android {
  compileSdk 29
  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs('src/main/java')
      aidl.srcDirs('src/main/java')
      renderscript.srcDirs('src/main/java')
      res.srcDirs('res')
      assets.srcDirs('../assets')
      jniLibs.srcDirs('libs')
    }
  }
  packagingOptions {
    resources.with {
      excludes += ['META-INF/robovm/ios/robovm.xml',
        'META-INF/DEPENDENCIES.txt', 'META-INF/DEPENDENCIES', 'META-INF/dependencies.txt', '**/*.gwt.xml']
      pickFirsts += ['META-INF/LICENSE.txt', 'META-INF/LICENSE', 'META-INF/license.txt', 'META-INF/LGPL2.1',
        'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/notice.txt']
    }
  }
  defaultConfig {
    applicationId 'emu.jvic'
    minSdkVersion 29
    targetSdkVersion 29
    versionCode 1
    versionName "${version}"
    multiDexEnabled true
  }
  namespace "emu.jvic"
  compileOptions {
    sourceCompatibility "8"
    targetCompatibility "8"
    coreLibraryDesugaringEnabled true
  }
  signingConfigs {
    release {
      storeFile file("keystore.jks")
      storePassword System.getenv("SIGNING_STORE_PASSWORD")
      keyAlias System.getenv("SIGNING_KEY_ALIAS")
      keyPassword System.getenv("SIGNING_KEY_PASSWORD")
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
}

repositories {
  // needed for AAPT2, may be needed for other tools
  google()
}

configurations { natives }

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
  implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
  implementation 'com.github.HBiSoft:PickiT:2.0.5'
  implementation project(':core')

  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

}

// Called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
tasks.register('copyAndroidNatives') {
  doFirst {
    file("libs/armeabi-v7a/").mkdirs()
    file("libs/arm64-v8a/").mkdirs()
    file("libs/x86_64/").mkdirs()
    file("libs/x86/").mkdirs()

    configurations.named("natives").orNull.copy().files.each { jar ->
      def outputDir = null
      if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
      if(jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
      if(jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
      if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
      if(outputDir != null) {
        copy {
          from zipTree(jar)
          into outputDir
          include "*.so"
        }
      }
    }
  }
}
tasks.matching { it.name.contains("merge") && it.name.contains("JniLibFolders") }.configureEach { packageTask ->
  packageTask.dependsOn 'copyAndroidNatives'
}

tasks.register('run', Exec) {
  def path
  def localProperties = project.file("../local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
      properties.load(instr)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    if (sdkDir) {
      path = sdkDir
    } else {
      path = "$System.env.ANDROID_SDK_ROOT"
    }
  } else {
    path = "$System.env.ANDROID_SDK_ROOT"
  }

  def adb = path + "/platform-tools/adb"
  commandLine "$adb", 'shell', 'am', 'start', '-n', 'emu.jvic/emu.jvic.android.AndroidLauncher'
}

eclipse.project.name = appName + "-android"
